---
layout: single
title: Project Euler Problem 11 in F#
date: 2010-09-21 16:03:25.000000000 +01:00
type: post
published: true
status: publish
categories:
- ".NET"
- F#
tags:
- algorithm
- F#
- functional
meta:
  ks_metadata: a:7:{s:4:"lang";s:2:"en";s:8:"keywords";s:48:"F#,fsharp,euler,algorithm,functional,composition";s:19:"keywords_autoupdate";s:1:"0";s:11:"description";s:44:"A
    solution to Project Euler Problem 11 in F#";s:22:"description_autoupdate";s:1:"0";s:5:"title";s:30:"Project
    Euler Problem 11 in F#";s:6:"robots";s:12:"index,follow";}
  _edit_last: '1'
  _aioseop_description: A solution to Project Euler Problem 11 in F#
  _aioseop_keywords: F#, fsharp, euler, algorithm, functional, composition
  _aioseop_title: Project Euler Problem 11 in F#
  _wp_old_slug: ''
  dsq_thread_id: '149554762'
---
<p><a href="http://projecteuler.net/index.php?section=problems&id=11">Project Euler Problem 11</a> is an interesting one; 
when addressed with a functional approach it lends itself to be solved using 
<i><a href="http://en.wikipedia.org/wiki/Function_composition_%28computer_science%29">function composition</a></i>. 
Let's have a look at the question and then a possible solution:</p>
<blockquote><p>In the 20x20 grid below, four numbers along a diagonal line have been marked in red.</p>
  
  08 02 22 97 38 15 00 40 00 75 04 05 07 78 52 12 50 77 91 08<br/> 
  49 49 99 40 17 81 18 57 60 87 17 40 98 43 69 48 04 56 62 00<br/> 
  81 49 31 73 55 79 14 29 93 71 40 67 53 88 30 03 49 13 36 65<br/> 
  52 70 95 23 04 60 11 42 69 24 68 56 01 32 56 71 37 02 36 91<br/> 
  22 31 16 71 51 67 63 89 41 92 36 54 22 40 40 28 66 33 13 80<br/>
  24 47 32 60 99 03 45 02 44 75 33 53 78 36 84 20 35 17 12 50<br/> 
  32 98 81 28 64 23 67 10 <span style="color: red;">26</span> 38 40 67 59 54 70 66 18 38 64 70<br/> 
  67 26 20 68 02 62 12 20 95 <span style="color: red;">63</span> 94 39 63 08 40 91 66 49 94 21<br/> 
  24 55 58 05 66 73 99 26 97 17 <span style="color: red;">78</span> 78 96 83 14 88 34 89 63 72<br/> 
  21 36 23 09 75 00 76 44 20 45 35 <span style="color: red;">14</span> 00 61 33 97 34 31 33 95<br/> 
  78 17 53 28 22 75 31 67 15 94 03 80 04 62 16 14 09 53 56 92<br/>
  16 39 05 42 96 35 31 47 55 58 88 24 00 17 54 24 36 29 85 57<br/> 
  86 56 00 48 35 71 89 07 05 44 44 37 44 60 21 58 51 54 17 58<br/> 
  19 80 81 68 05 94 47 69 28 73 92 13 86 52 17 77 04 89 55 40<br/> 
  04 52 08 83 97 35 99 16 07 97 57 32 16 26 26 79 33 27 98 66<br/> 
  88 36 68 87 57 62 20 72 03 46 33 67 46 55 12 32 63 93 53 69<br/> 
  04 42 16 73 38 25 39 11 24 94 72 18 08 46 29 32 40 62 76 36<br/> 
  20 69 36 41 72 30 23 88 34 62 99 69 82 67 59 85 74 04 36 16<br/> 
  20 73 35 29 78 31 90 01 74 31 49 71 48 86 81 16 23 57 05 54<br/> 
  01 70 54 71 83 51 54 69 16 92 33 48 61 43 52 01 89 19 67 48<br/> 
  
  <p></p>
  <p>The product of these numbers is 26 x 63 x 78 x 14 = 1788696.</p> 
   
  <p>What is the greatest product of four adjacent numbers 
  in any direction (up, down, left, right, or diagonally) in the 20x20 grid?</p></blockquote>

{% highlight fsharp %}

let numbers =
    array2D [|
      [|08;02;22;97;38;15;00;40;00;75;04;05;07;78;52;12;50;77;91;08|];
      [|49;49;99;40;17;81;18;57;60;87;17;40;98;43;69;48;04;56;62;00|];
      [|81;49;31;73;55;79;14;29;93;71;40;67;53;88;30;03;49;13;36;65|];
      [|52;70;95;23;04;60;11;42;69;24;68;56;01;32;56;71;37;02;36;91|];
      [|22;31;16;71;51;67;63;89;41;92;36;54;22;40;40;28;66;33;13;80|];
      [|24;47;32;60;99;03;45;02;44;75;33;53;78;36;84;20;35;17;12;50|];
      [|32;98;81;28;64;23;67;10;26;38;40;67;59;54;70;66;18;38;64;70|];
      [|67;26;20;68;02;62;12;20;95;63;94;39;63;08;40;91;66;49;94;21|];
      [|24;55;58;05;66;73;99;26;97;17;78;78;96;83;14;88;34;89;63;72|];
      [|21;36;23;09;75;00;76;44;20;45;35;14;00;61;33;97;34;31;33;95|];
      [|78;17;53;28;22;75;31;67;15;94;03;80;04;62;16;14;09;53;56;92|];
      [|16;39;05;42;96;35;31;47;55;58;88;24;00;17;54;24;36;29;85;57|];
      [|86;56;00;48;35;71;89;07;05;44;44;37;44;60;21;58;51;54;17;58|];
      [|19;80;81;68;05;94;47;69;28;73;92;13;86;52;17;77;04;89;55;40|];
      [|04;52;08;83;97;35;99;16;07;97;57;32;16;26;26;79;33;27;98;66|];
      [|88;36;68;87;57;62;20;72;03;46;33;67;46;55;12;32;63;93;53;69|];
      [|04;42;16;73;38;25;39;11;24;94;72;18;08;46;29;32;40;62;76;36|];
      [|20;69;36;41;72;30;23;88;34;62;99;69;82;67;59;85;74;04;36;16|];
      [|20;73;35;29;78;31;90;01;74;31;49;71;48;86;81;16;23;57;05;54|];
      [|01;70;54;71;83;51;54;69;16;92;33;48;61;43;52;01;89;19;67;48|];
      |]
    

let dimensions = (Array2D.length1 numbers, Array2D.length2 numbers)

let horizontal_traverser (i, j) (iMax, jMax) f_4 =
    if (i > iMax) || (j + 4 > jMax) then 0
    else f_4 numbers.[i,j]  numbers.[i, j+1] numbers.[i,j+2] numbers.[i,j+3]

let vertical_traverser (i,j) (iMax, jMax) f_4 =
    if (i + 4 > iMax) || (j > jMax) then 0
    else f_4 numbers.[i,j] numbers.[i+1, j] numbers.[i+2, j] numbers.[i+3,j]

let diagonal_traverser_left_right (i, j) (iMax, jMax) f_4 =
    if (i + 4 > iMax) || (j + 4 > jMax) then 0
    else f_4 numbers.[i,j] numbers.[i+1, j+1] numbers.[i+2, j+2] numbers.[i+3,j+3]

let diagonal_traverser_right_left (i, j) (iMax, jMax) f_4 =
    if (i - 3 &lt; 0) || (j + 4 > jMax) then 0
    else f_4 numbers.[i,j] numbers.[i-1, j+1] numbers.[i-2, j+2] numbers.[i-3,j+3]

let max_in_direction traverser f_4 =
    seq { for i in 0..fst dimensions - 1 do
              for j in 0..snd dimensions - 1 do
                  yield traverser (i,j) dimensions f_4 }
    |> Seq.max

let maximum f_4 =
    [| max_in_direction horizontal_traverser f_4;
       max_in_direction vertical_traverser f_4;
       max_in_direction diagonal_traverser_left_right f_4;
       max_in_direction diagonal_traverser_right_left f_4 |]
    |> Array.max

let problem_11 () =
    maximum (fun a b c d -> a * b * c * d)

{% endhighlight %}

<ul>
<li>We first declare a two-dimensions array containing the 20x20 numbers that we need to explore. 
  To do this we use F# <a href="http://msdn.microsoft.com/en-us/library/ee353794.aspx">Array2D Module</a>. 
  The same module is used also to compute a tuple containing the size of the two dimension arrays, which we'll use later on.</li>
<li>We then proceed to define four <i>traverser</i> functions, one for each of these directions: horizontal, 
vertical, diagonal top-left to bottom-right and diagonal bottom-left to top-right. Each 
traverser takes as input a given item of the array and applies the supplied 4-parameters 
function to the starting item plus the next 3 items in a direction. Note how at this point 
we are not talking about a specific operation (product or sum) yet and we are concentrating 
on 4 items of the array at a time.</li>
<li>Next we define the <code>max_in_direction</code> function that takes a 
traverser and a 4-parameters function and finds the maximum value in 
that particular direction (whatever it might be applying the given function).</li>
<li>We are getting closer to the solution by defining the <code>maximum</code> one-dimension array, 
where each item is the maximum value in each of the four directions and then taking the highest one.</li>
<li>We finally solve the problem by invoking the <code>maximum</code> 
function passing to it the product of four items as a lambda.</li>
</ul>
<p>Note how easy it is to change the particular function that we use to
   aggregate the 4 items: suppose for example that we are interested in the 4 items 
   giving the maximum <i>sum</i>, we could simply invoke:</p>

{% highlight F# %}
maximum (fun a b c d -> a + b + c + d)
{% endhighlight %}
